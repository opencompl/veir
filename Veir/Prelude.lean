module

public import Lean
public import Veir.PrePrelude
meta import Veir.PrePrelude

public section

@[simp, grind norm]
theorem UInt64.add_int64_zero (x : UInt64) : x + (0 : Int64) = x := by
  simp [UInt64.add_int64_r_def]

@[simp, grind norm]
theorem UInt64.uint64_zero_add (x : Int64) : (0 : UInt64) + x = x.toUInt64 := by
  simp [UInt64.add_int64_r_def]

open Lean Meta Simp in
def mkInst : Expr := mkConst  `instHAddInt64UInt64_veir []
open Lean Meta Simp in
def mkInst' : Expr := mkConst `instHAddUInt64Int64_veir []

builtin_dsimproc_decl normNatAddInst ((_ + _ : Int64)) :=
  Lean.Meta.Grind.Arith.normInst 3 mkInst
builtin_dsimproc_decl normNatAddInst' ((_ + _ : UInt64)) :=
  Lean.Meta.Grind.Arith.normInst 3 mkInst'

attribute [seval] normNatAddInst normNatAddInst'

open Lean Meta Simp

private meta def uint64Lit? (e : Expr) : Option UInt64 := do
  return UInt64.ofNat (← e.nat?)

private meta def int64Lit? (e : Expr) : Option Int64 :=
  match_expr e with
  | OfNat.ofNat _ _ _ =>
    (Int64.ofNat ·) <$> e.nat?
  | Neg.neg _ _ x => do
    let_expr OfNat.ofNat _ _ _ := x | none
    return -Int64.ofNat (← e.int?.map (·.toNat))
  | _ => none

dsimproc_decl reduceMixedAdd ((_ : Int64) + (_ : UInt64)) := fun e => do
  let_expr HAdd.hAdd _ _ _ _ x y := e | return .continue
  let some xv := int64Lit? x | return .continue
  let some yv := uint64Lit? y | return .continue
  return .done (toExpr (xv + yv))

attribute [seval] reduceMixedAdd

-- TODO: Move in prelude?
instance : HAdd Nat (Std.Rco Int) (Std.Rco Int) where
  hAdd x y := (x + y.lower)...(x + y.upper)

@[grind =, layout_simp]
theorem add_nat_range_def (n : Nat) (r : Std.Rco Int) : n + r = (n+r.lower)...(n + r.upper) := rfl

attribute [grind norm] add_nat_range_def

/-- When an array's size fits in a `UInt64`, `size.toUInt64` round-trips back to `size`.
Unlike `Array.usize` (which truncates mod the platform-dependent `USize.size`), this uses
`size.toUInt64` and is bounded by the platform-independent `UInt64.size`. -/
theorem Array.size_toUInt64_toNat (ar : Array α) (h : ar.size < UInt64.size) :
    ar.size.toUInt64.toNat = ar.size := by
  simp only [Nat.toUInt64_eq, UInt64.toNat_ofNat']
  grind [Nat.mod_eq_of_lt]

theorem Array.size_le_toNat {ar : Array α} {x : UInt64}
    (hsz : ar.size < UInt64.size) (h : ar.size.toUInt64 ≤ x) : ar.size ≤ x.toNat := by
  have := Array.size_toUInt64_toNat ar hsz
  rw [← this]
  grind [UInt64.le_iff_toNat_le]

/-- An array's size as a `UInt64`. This is a deliberately opaque (`@[irreducible]`) alias for
`size.toUInt64`, so it does not reduce to the `UInt64.ofNat _` shape nor further to `size % 2^64`.
`grind` therefore treats `sizeU64`/`sizeU64.toNat` as atoms rather than e-matching candidates for
the `index`/`numArgs`-shaped `UInt64` parameters (and their `.toNat`) of the `init*`/`allocEmpty`
lemmas — avoiding the combinatorial instantiation blow-up that a bare `size.toUInt64` triggers when
several are in scope at once. Unfold to `size.toUInt64` via `Array.sizeU64_eq`. -/
@[irreducible] def Array.sizeU64 (ar : Array α) : UInt64 := ar.size.toUInt64

theorem Array.sizeU64_eq (ar : Array α) : ar.sizeU64 = ar.size.toUInt64 := by
  unfold Array.sizeU64
  rfl

theorem Array.sizeU64_toNat (ar : Array α) (h : ar.size < UInt64.size) :
    ar.sizeU64.toNat = ar.size := by
  rw [sizeU64_eq]
  exact ar.size_toUInt64_toNat h

macro:50 "rlet" pat:term ":=" expr:term rest:term : term =>
  `(match _ : $expr:term with
      | $pat => $rest)

macro:50 "rlet" h:ident ":" pat:term ":=" expr:term rest:term : term =>
  `(match $h:ident : $expr:term with
      | $pat => $rest)

macro:50 "rlet" pat:term "←" expr:term  rest:term : term =>
  `(match _ : $expr:term with
      | none => none
      | some $pat => $rest)

macro:50 "rlet" h:ident ":" pat:term "←" expr:term  rest:term : term =>
  `(match $h:ident : $expr:term with
      | none => none
      | some $pat => $rest)

-- TODO: remove this
macro:50 "rlet" pat:term "←" expr:term "in" rest:term : term =>
  `(match _ : $expr:term with
      | none => none
      | some $pat => $rest)

def Option.maybe (p : α → β → Prop) (x : Option α) (y : β) : Prop :=
  ∀ z, x = some z → p z y

theorem Option.maybe_def (p : α → β → Prop) (x : Option α) (y : β) :
    x.maybe p y ↔ ∀ z, x = some z → p z y := by
  simp [Option.maybe]

@[simp, grind =] theorem Option.maybe_some : (some x).maybe p y ↔ p x y := by grind [maybe]
@[simp, grind .] theorem Option.maybe_none : none.maybe p y := by grind [maybe]

def Option.maybe₁ (p : α → Prop) (x : Option α)  : Prop :=
  ∀ z, x = some z → p z

theorem Option.maybe₁_def (p : α → Prop) (x : Option α) :
    x.maybe₁ p ↔ ∀ z, x = some z → p z := by
  simp [Option.maybe₁]

@[simp, grind =] theorem Option.maybe₁_some : (some x).maybe₁ p ↔ p x := by grind [maybe₁]
@[simp, grind .] theorem Option.maybe₁_none : none.maybe₁ p := by grind [maybe₁]
